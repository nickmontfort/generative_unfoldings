//this file is originally based on one from https://github.com/aferriss/p5jsShaderExamples

precision mediump float;

// our texcoords from the vertex shader
varying vec2 vTexCoord;

// the texture that we want to manipulate
uniform sampler2D tex0;

uniform float game_time;

uniform vec3 alt_col256;


/* 3d simplex noise */
//https://www.shadertoy.com/view/XsX3zB
vec3 random3(vec3 c) {
  float j = 4096.0*sin(dot(c,vec3(17.0, 59.4, 15.0)));
  vec3 r;
  r.z = fract(512.0*j);
  j *= .125;
  r.x = fract(512.0*j);
  j *= .125;
  r.y = fract(512.0*j);
  return r-0.5;
}

/* skew constants for 3d simplex functions */
const float F3 =  0.3333333;
const float G3 =  0.1666667;

/* 3d simplex noise */
float simplex3d(vec3 p) {
   /* 1. find current tetrahedron T and it's four vertices */
   /* s, s+i1, s+i2, s+1.0 - absolute skewed (integer) coordinates of T vertices */
   /* x, x1, x2, x3 - unskewed coordinates of p relative to each of T vertices*/
   
   /* calculate s and x */
   vec3 s = floor(p + dot(p, vec3(F3)));
   vec3 x = p - s + dot(s, vec3(G3));
   
   /* calculate i1 and i2 */
   vec3 e = step(vec3(0.0), x - x.yzx);
   vec3 i1 = e*(1.0 - e.zxy);
   vec3 i2 = 1.0 - e.zxy*(1.0 - e);
    
   /* x1, x2, x3 */
   vec3 x1 = x - i1 + G3;
   vec3 x2 = x - i2 + 2.0*G3;
   vec3 x3 = x - 1.0 + 3.0*G3;
   
   /* 2. find four surflets and store them in d */
   vec4 w, d;
   
   /* calculate surflet weights */
   w.x = dot(x, x);
   w.y = dot(x1, x1);
   w.z = dot(x2, x2);
   w.w = dot(x3, x3);
   
   /* w fades from 0.6 at the center of the surflet to 0.0 at the margin */
   w = max(0.6 - w, 0.0);
   
   /* calculate surflet components */
   d.x = dot(random3(s), x);
   d.y = dot(random3(s + i1), x1);
   d.z = dot(random3(s + i2), x2);
   d.w = dot(random3(s + 1.0), x3);
   
   /* multiply d by w^4 */
   w *= w;
   w *= w;
   d *= w;
   
   /* 3. return the sum of the four surflets */
   return dot(d, vec4(52.0));
}

void main(){

  vec2 uv = vTexCoord;
  // flip the y uvs
  uv.y = 1.0 - uv.y;

  vec4 bg_col = texture2D(tex0, vec2(0,0));
  vec4 color = texture2D(tex0, uv);

    
  float noise_zoom = 20.0;
  float noise_speed = 15.0;
  float noise_prc = 1.0;

  vec4 end_col = vec4(color.rgb,1.0);

  //non background colors
  if ( abs(color.r-bg_col.r) > 0.01 || abs(color.g-bg_col.g) > 0.01 || abs(color.b-bg_col.b) > 0.01){
    //do nothing
  }
  //background
  else{
    noise_zoom *= 0.2;
    noise_speed *= 0.1;
    noise_prc =  0.3 + 0.7 * simplex3d( vec3(uv.x*noise_zoom, uv.y*noise_zoom, game_time*noise_speed));

    float noise_grey = 1.0;

    vec4 alt_col = vec4(alt_col256.r/255.0, alt_col256.g/255.0, alt_col256.b/255.0, 1.0);

    if ( floor( mod(noise_prc*255.0, 10.0) ) < 1.0 ){
      end_col = noise_prc * vec4(color.rgb,1.0) + (1.0-noise_prc) * alt_col;
    }
  }
  
  
  gl_FragColor = end_col;

}


